int state = 0;
int subdivisions_per_axis = 2;
const float PI = 3.1415926;

// 0..1
float pgc_random(inout uint seed) {
    // minimal PCG32 code / (c) 2014 M.E. O'Neill / pcg-random.org
    // Licensed under Apache License 2.0 (NO WARRANTY, etc. see website)
    seed = seed * 747796405u + 2891336453u;
    uint word = ((seed >> ((seed >> 28u) + 4u)) ^ seed) * 277803737u;
    return float(word) / 4294967295.0;
}

// this subdivides the randomness into quadrants as a form of basic low-discrepancy noise.
// might be expensive.
vec2 pixel_offset(inout uint seed) {
    float offset = 1.0 / float(subdivisions_per_axis);

    float x = pgc_random(seed) * offset;
    float y = pgc_random(seed) * offset;

    x += float(state % subdivisions_per_axis) * offset;
    y += float(state / subdivisions_per_axis) * offset;

    state = (state + 1) % (subdivisions_per_axis * subdivisions_per_axis);
    return vec2(x - 0.5, y - 0.5);
}

// https://www.semanticscholar.org/paper/A-Note-on-the-Generation-of-Random-Normal-Deviates-Box-Muller/bdbf5ecc09facb88308f2325dbf0b458f812ab2e
float random_normal(inout uint seed) {
    float a = sqrt(-2.0 * log(1.0f - pgc_random(seed)));
    float b = 2.0 * PI * pgc_random(seed);

    return cos(b) * a;
}

// Since normal distributions are spherically symmetric, sampling it for each coordinate
//  generates vectors uniformly distributed within the sphere.
vec3 random_unit_vector(inout uint seed) {
    vec3 random = vec3(random_normal(seed), random_normal(seed), random_normal(seed));
    return normalize(random);
}

vec3 random_unit_in_hemisphere(inout uint seed, in vec3 normal) {
    vec3 vector = random_unit_vector(seed);

    if (dot(vector, normal) > 0.0) {
        return vector;
    } else {
        return -vector;
    }
}